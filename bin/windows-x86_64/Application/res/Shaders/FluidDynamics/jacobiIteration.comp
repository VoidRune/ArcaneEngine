#version 450

layout(set = 0, binding = 0, rg16f) uniform image2D inputDivergencePressure;
layout(set = 0, binding = 1, rg16f) uniform image2D outputDivergencePressure;

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

void main()
{	
	ivec2 px = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(inputDivergencePressure);

	ivec2 l = ivec2(max(px.x-1,0), px.y);
    ivec2 r = ivec2(min(px.x+1,size.x-1), px.y);
    ivec2 b = ivec2(px.x, max(px.y-1,0));
    ivec2 t = ivec2(px.x, min(px.y+1,size.y-1));

	float dl = imageLoad(inputDivergencePressure, l).y;
	float dr = imageLoad(inputDivergencePressure, r).y;
	float db = imageLoad(inputDivergencePressure, b).y;
	float dt = imageLoad(inputDivergencePressure, t).y;

	float divergence = imageLoad(inputDivergencePressure, px).x;
	float pressure = (dl + dr + db + dt - divergence) * 0.25;

	imageStore(outputDivergencePressure, px, vec4(divergence, pressure, 0, 0));
}